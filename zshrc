# ~/.zshrc file for zsh interactive shells.

setopt autocd              # change directory just by typing its name
#setopt correct            # auto correct mistakes
setopt interactivecomments # allow comments in interactive mode
setopt magicequalsubst     # enable filename expansion for arguments of the form 'anything=expression'
setopt nonomatch           # hide error message if there is no match for the pattern
setopt notify              # report the status of background jobs immediately
setopt numericglobsort     # sort filenames numerically when it makes sense
setopt promptsubst         # enable command substitution in prompt

WORDCHARS=${WORDCHARS//\/} # Don't consider certain characters part of the word

# hide EOL sign ('%')
PROMPT_EOL_MARK=""

# configure key keybindings
bindkey -e                                        # emacs key bindings
bindkey ' ' magic-space                           # do history expansion on space
bindkey '^[[3;5~' kill-word                       # ctrl + Supr
bindkey '^[[3~' delete-char                       # delete
bindkey '^[[1;5C' forward-word                    # ctrl + ->
bindkey '^[[1;5D' backward-word                   # ctrl + <-
bindkey '^[[5~' beginning-of-buffer-or-history    # page up
bindkey '^[[6~' end-of-buffer-or-history          # page down
bindkey '^[[H' beginning-of-line                  # home
bindkey '^[[F' end-of-line                        # end
bindkey '^[[Z' undo                               # shift + tab undo last action

# enable completion features
autoload -Uz compinit
compinit -d ~/.cache/zcompdump
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' rehash true
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

# History configurations
HISTFILE=~/.zsh_history
HISTSIZE=1000
SAVEHIST=2000
setopt hist_expire_dups_first # delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt hist_ignore_dups       # ignore duplicated commands history list
setopt hist_ignore_space      # ignore commands that start with space
setopt hist_verify            # show command with history expansion to user before running it
#setopt share_history         # share command history data

# force zsh to show the complete history
alias history="history 0"

# configure `time` format
TIMEFMT=$'\nreal\t%E\nuser\t%U\nsys\t%S\ncpu\t%P'

# make less more friendly for non-text input files, see lesspipe(1)
[ -x /usr/bin/lesspipe ] && eval "$(SHELL=/bin/sh lesspipe)"

# set a fancy prompt (non-color, unless we know we "want" color)
case "$TERM" in
    xterm-color|*-256color) color_prompt=yes;;
esac

# uncomment for a colored prompt, if the terminal has the capability; turned
# off by default to not distract the user: the focus in a terminal window
# should be on the output of commands, not on the prompt
force_color_prompt=yes

if [ -n "$force_color_prompt" ]; then
    if [ -x /usr/bin/tput ] && tput setaf 1 >&/dev/null; then
        # We have color support; assume it's compliant with Ecma-48
        # (ISO/IEC-6429). (Lack of such support is extremely rare, and such
        # a case would tend to support setf rather than setaf.)
        color_prompt=yes
    else
        color_prompt=
    fi
fi

configure_prompt() {
    prompt_symbol=„âø
    [ "$EUID" -eq 0 ] && prompt_symbol=üíÄ
    case "$PROMPT_ALTERNATIVE" in
        twoline)
            PROMPT=$'%F{%(#.blue.green)}‚îå‚îÄ‚îÄ${VIRTUAL_ENV:+($(basename $VIRTUAL_ENV))‚îÄ}(%B%F{%(#.red.blue)}%n$prompt_symbol%m%b%F{%(#.blue.green)})-[%B%F{reset}%(6~.%-1~/‚Ä¶/%4~.%5~)%b%F{%(#.blue.green)}]\n‚îî‚îÄ%B%(#.%F{red}#.%F{blue}$)%b%F{reset} '
            RPROMPT=$'%(?.. %? %F{red}%B‚®Ø%b%F{reset})%(1j. %j %F{yellow}%B‚öô%b%F{reset}.)'
            ;;
        oneline)
            PROMPT=$'${VIRTUAL_ENV:+($(basename $VIRTUAL_ENV))}%B%F{%(#.red.blue)}%n@%m%b%F{reset}:%B%F{%(#.blue.green)}%~%b%F{reset}%(#.#.$) '
            RPROMPT=
            ;;
        backtrack)
            PROMPT=$'${VIRTUAL_ENV:+($(basename $VIRTUAL_ENV))}%B%F{red}%n@%m%b%F{reset}:%B%F{blue}%~%b%F{reset}%(#.#.$) '
            RPROMPT=
            ;;
    esac
}

# The following block is surrounded by two delimiters.
# These delimiters must not be modified. Thanks.
# START KALI CONFIG VARIABLES
PROMPT_ALTERNATIVE=twoline
NEWLINE_BEFORE_PROMPT=yes
# STOP KALI CONFIG VARIABLES

if [ "$color_prompt" = yes ]; then
    # override default virtualenv indicator in prompt
    VIRTUAL_ENV_DISABLE_PROMPT=1

    configure_prompt

    # enable syntax-highlighting
    if [ -f /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ] && [ "$color_prompt" = yes ]; then
        . /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
        ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern)
        ZSH_HIGHLIGHT_STYLES[default]=none
        ZSH_HIGHLIGHT_STYLES[unknown-token]=fg=red,bold
        ZSH_HIGHLIGHT_STYLES[reserved-word]=fg=cyan,bold
        ZSH_HIGHLIGHT_STYLES[suffix-alias]=fg=green,underline
        ZSH_HIGHLIGHT_STYLES[global-alias]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[precommand]=fg=green,underline
        ZSH_HIGHLIGHT_STYLES[commandseparator]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[autodirectory]=fg=green,underline
        ZSH_HIGHLIGHT_STYLES[path]=underline
        ZSH_HIGHLIGHT_STYLES[path_pathseparator]=
        ZSH_HIGHLIGHT_STYLES[path_prefix_pathseparator]=
        ZSH_HIGHLIGHT_STYLES[globbing]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[history-expansion]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[command-substitution]=none
        ZSH_HIGHLIGHT_STYLES[command-substitution-delimiter]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[process-substitution]=none
        ZSH_HIGHLIGHT_STYLES[process-substitution-delimiter]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[single-hyphen-option]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[double-hyphen-option]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[back-quoted-argument]=none
        ZSH_HIGHLIGHT_STYLES[back-quoted-argument-delimiter]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[single-quoted-argument]=fg=yellow
        ZSH_HIGHLIGHT_STYLES[double-quoted-argument]=fg=yellow
        ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument]=fg=yellow
        ZSH_HIGHLIGHT_STYLES[rc-quote]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[dollar-double-quoted-argument]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[back-double-quoted-argument]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[back-dollar-quoted-argument]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[assign]=none
        ZSH_HIGHLIGHT_STYLES[redirection]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[comment]=fg=black,bold
        ZSH_HIGHLIGHT_STYLES[named-fd]=none
        ZSH_HIGHLIGHT_STYLES[numeric-fd]=none
        ZSH_HIGHLIGHT_STYLES[arg0]=fg=green
        ZSH_HIGHLIGHT_STYLES[bracket-error]=fg=red,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-1]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-2]=fg=green,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-3]=fg=magenta,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-4]=fg=yellow,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-5]=fg=cyan,bold
        ZSH_HIGHLIGHT_STYLES[cursor-matchingbracket]=standout
    elif [ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ] && [ "$color_prompt" = yes ]; then
        . /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
        ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern)
        ZSH_HIGHLIGHT_STYLES[default]=none
        ZSH_HIGHLIGHT_STYLES[unknown-token]=fg=red,bold
        ZSH_HIGHLIGHT_STYLES[reserved-word]=fg=cyan,bold
        ZSH_HIGHLIGHT_STYLES[suffix-alias]=fg=green,underline
        ZSH_HIGHLIGHT_STYLES[global-alias]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[precommand]=fg=green,underline
        ZSH_HIGHLIGHT_STYLES[commandseparator]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[autodirectory]=fg=green,underline
        ZSH_HIGHLIGHT_STYLES[path]=underline
        ZSH_HIGHLIGHT_STYLES[path_pathseparator]=
        ZSH_HIGHLIGHT_STYLES[path_prefix_pathseparator]=
        ZSH_HIGHLIGHT_STYLES[globbing]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[history-expansion]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[command-substitution]=none
        ZSH_HIGHLIGHT_STYLES[command-substitution-delimiter]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[process-substitution]=none
        ZSH_HIGHLIGHT_STYLES[process-substitution-delimiter]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[single-hyphen-option]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[double-hyphen-option]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[back-quoted-argument]=none
        ZSH_HIGHLIGHT_STYLES[back-quoted-argument-delimiter]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[single-quoted-argument]=fg=yellow
        ZSH_HIGHLIGHT_STYLES[double-quoted-argument]=fg=yellow
        ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument]=fg=yellow
        ZSH_HIGHLIGHT_STYLES[rc-quote]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[dollar-double-quoted-argument]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[back-double-quoted-argument]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[back-dollar-quoted-argument]=fg=magenta
        ZSH_HIGHLIGHT_STYLES[assign]=none
        ZSH_HIGHLIGHT_STYLES[redirection]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[comment]=fg=black,bold
        ZSH_HIGHLIGHT_STYLES[named-fd]=none
        ZSH_HIGHLIGHT_STYLES[numeric-fd]=none
        ZSH_HIGHLIGHT_STYLES[arg0]=fg=green
        ZSH_HIGHLIGHT_STYLES[bracket-error]=fg=red,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-1]=fg=blue,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-2]=fg=green,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-3]=fg=magenta,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-4]=fg=yellow,bold
        ZSH_HIGHLIGHT_STYLES[bracket-level-5]=fg=cyan,bold
        ZSH_HIGHLIGHT_STYLES[cursor-matchingbracket]=standout
    fi
else
    PROMPT='%n@%m:%~%# '
fi
unset color_prompt force_color_prompt

toggle_oneline_prompt(){
    if [ "$PROMPT_ALTERNATIVE" = oneline ]; then
        PROMPT_ALTERNATIVE=twoline
    else
        PROMPT_ALTERNATIVE=oneline
    fi
    configure_prompt
    zle reset-prompt
}
zle -N toggle_oneline_prompt
bindkey ^P toggle_oneline_prompt

# If this is an xterm set the title to user@host:dir
case "$TERM" in
xterm*|rxvt*|Eterm|aterm|kterm|gnome*|alacritty)
    TERM_TITLE=$'\e]0;${VIRTUAL_ENV:+($(basename $VIRTUAL_ENV))}%n@%m: %~\a'
    ;;
*)
    ;;
esac

precmd() {
    # Print the previously configured title
    print -Pnr -- "$TERM_TITLE"

    # Print a new line before the prompt, but only if it is not the first line
    if [ "$NEWLINE_BEFORE_PROMPT" = yes ]; then
        if [ -z "$_NEW_LINE_BEFORE_PROMPT" ]; then
            _NEW_LINE_BEFORE_PROMPT=1
        else
            print ""
        fi
    fi
}


# enable color support of ls, less and man, and also add handy aliases
if [ -x /usr/bin/dircolors ]; then
    test -r ~/.dircolors && eval "$(dircolors -b ~/.dircolors)" || eval "$(dircolors -b)"
    alias ls='ls --color=auto'
    #alias dir='dir --color=auto'
    #alias vdir='vdir --color=auto'

    alias grep='grep --color=auto'
    alias fgrep='fgrep --color=auto'
    alias egrep='egrep --color=auto'
    alias diff='diff --color=auto'
    alias ip='ip --color=auto'

    export LESS_TERMCAP_mb=$'\E[1;31m'     # begin blink
    export LESS_TERMCAP_md=$'\E[1;36m'     # begin bold
    export LESS_TERMCAP_me=$'\E[0m'        # reset bold/blink
    export LESS_TERMCAP_so=$'\E[01;33m'    # begin reverse video
    export LESS_TERMCAP_se=$'\E[0m'        # reset reverse video
    export LESS_TERMCAP_us=$'\E[1;32m'     # begin underline
    export LESS_TERMCAP_ue=$'\E[0m'        # reset underline

    # Take advantage of $LS_COLORS for completion as well
    zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
    zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
fi


# enable auto-suggestions based on the history
if [ -f /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh ]; then
    . /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh
    # change suggestion color
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=#999'
elif [ -f /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]; then
    . /usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh
    # change suggestion color
    ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=#999'
fi

# Arch Linux specific command-not-found handler
if [ -f /usr/share/doc/pkgfile/command-not-found.zsh ]; then
    . /usr/share/doc/pkgfile/command-not-found.zsh
fi

###########     CUSTOM FUNCTIONS     ##########

# cd show the folder content:
function cd() {
  builtin cd "$@" && ls
}


# realiza a atualiza√ß√£o completa e limpexa de cache
function full-upgrade(){
    yay -Syyuu --noconfirm
    yay -Scc --noconfirm
    local orphans=$(pacman -Qdtq)
    if [[ -n "$orphans" ]]; then
      echo "Removendo √≥rf√£os: $orphans"
      sudo pacman -Rs $orphans --noconfirm
    else
      echo "Nenhum pacote √≥rf√£o encontrado üéâ"
    fi
}


# Ativa√ß√£o do tailscale automatiocamente ao digitar o comando
ts-up() {
  echo "[ts-up] Iniciando tailscaled (systemd)‚Ä¶"
  sudo systemctl enable --now tailscaled

  # espera o socket/daemon ficar pronto (at√© ~5s)
  for i in {1..10}; do
    if sudo systemctl is-active --quiet tailscaled && [ -S /var/run/tailscale/tailscaled.sock ]; then
      break
    fi
    sleep 0.5
  done

  if ! sudo systemctl is-active --quiet tailscaled; then
    echo "[ts-up] ERRO: tailscaled n√£o ficou ativo. √öltimos logs:"
    journalctl -u tailscaled -b -n 60 --no-pager
    return 1
  fi

  echo "[ts-up] Executando: tailscale up $*"
  sudo tailscale up "$@" || {
    echo "[ts-up] Falhou o 'tailscale up'. Logs recentes:"
    journalctl -u tailscaled -b -n 60 --no-pager
    return 1
  }

  echo "[ts-up] OK. Status:"
  sudo tailscale status
}

ts-down() {
  echo "[ts-down] tailscale down‚Ä¶"
  sudo tailscale down || true

  echo "[ts-down] Parando tailscaled (systemd)‚Ä¶"
  sudo systemctl stop tailscaled

  # mata qualquer processo residual e limpa socket
  sudo pkill -x tailscaled 2>/dev/null || true
  sudo rm -f /var/run/tailscale/tailscaled.sock

  echo "[ts-down] Feito."
}

function ping() {
  emulate -L zsh
  setopt localoptions noxtrace
  PS4=

  local no_dns=0 host logfile interval out line rc
  [[ "$1" == "-n" ]] && { no_dns=1; shift; }

  host="$1"; logfile="$2"; interval="${3:-1}"
  if [[ -z "$host" ]]; then
    print "uso: ping [-n] <host|ip> [logfile] [intervalo]"
    return 1
  fi

  # timeouts (s)
  local RESOLVE_TIMEOUT="${RESOLVE_TIMEOUT:-2}"
  local DEADLINE="${PING_TIMEOUT:-3}"
  local TIMEOUT_BIN="/usr/bin/timeout"
  [[ ! -x "$TIMEOUT_BIN" ]] && TIMEOUT_BIN=""

  # √© IPv4 literal?
  local is_ipv4=0
  [[ "$host" =~ ^[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+$ ]] && is_ipv4=1

  while true; do
    # 1) DNS (se n√£o for IP)
    if (( ! is_ipv4 )); then
      if [[ -n "$TIMEOUT_BIN" ]]; then
        $TIMEOUT_BIN "$RESOLVE_TIMEOUT" getent hosts "$host" >/dev/null 2>&1
      else
        getent hosts "$host" >/dev/null 2>&1
      fi
      if [[ $? -ne 0 ]]; then
        printf '%b\n' "$(date '+%F %T')  \033[31m[DNS]\033[0m host n√£o resolvido: $host"
        [[ -n "$logfile" ]] && printf '%s  [DNS] host n√£o resolvido: %s\n' "$(date '+%F %T')" "$host" >> "$logfile"
        sleep "$interval"
        continue
      fi
    fi

    # 2) executa o ping (1 tentativa; timeout total DEADLINE)
    local cmd=( /bin/ping -c1 -W1 )
    (( no_dns )) && cmd+=( -n )
    cmd+=( "$host" )

    if [[ -n "$TIMEOUT_BIN" ]]; then
      out="$($TIMEOUT_BIN "$DEADLINE" "${cmd[@]}" 2>&1)"; rc=$?
    else
      out="$("${cmd[@]}" 2>&1)"; rc=$?
    fi

    if [[ $rc -eq 0 ]]; then
      # extrai a linha ‚Äú64 bytes ‚Ä¶‚Äù
      line=$(print -- "$out" | awk '/icmp_seq=.*(time|tempo)=/ {print; exit}')
      [[ -z "$line" ]] && line=$(print -- "$out" | awk '/bytes .*icmp_seq=/ {print; exit}')
      if [[ -n "$line" ]]; then
        # coloriza mantendo formato
        line=$(printf '%s\n' "$line" | awk '{
          l=$0
          gsub(/^[0-9]+ bytes/,"\033[1;37m&\033[0m",l)
          gsub(/de [^ ]+/,"\033[36m&\033[0m",l)
          gsub(/icmp_seq=[0-9]+/,"\033[35m&\033[0m",l)
          gsub(/ttl=[0-9]+/,"\033[34m&\033[0m",l)
          gsub(/(tempo|time)=[0-9.]+ ms/,"\033[33m&\033[0m",l)
          print l
        }')
        printf '%b\n' "$(date '+%F %T')  $line"
        [[ -n "$logfile" ]] && printf '%s  %s\n' "$(date '+%F %T')" "$(echo "$line" | sed -E 's/\x1B\[[0-9;]*m//g')" >> "$logfile"
      fi
    else
      # distingue timeout geral de outras falhas
      if [[ $rc -eq 124 || $rc -eq 137 ]]; then
        printf '%b\n' "$(date '+%F %T')  \033[31m[FAIL]\033[0m sem resposta de $host (timeout ${DEADLINE}s)"
        [[ -n "$logfile" ]] && printf '%s  [FAIL] sem resposta de %s (timeout %ss)\n' "$(date '+%F %T')" "$host" "$DEADLINE" >> "$logfile"
      else
        printf '%b\n' "$(date '+%F %T')  \033[31m[FAIL]\033[0m sem resposta de $host"
        [[ -n "$logfile" ]] && printf '%s  [FAIL] sem resposta de %s\n' "$(date '+%F %T')" "$host" >> "$logfile"
      fi
    fi

    sleep "$interval"
  done
}

###########     CUSTOM ALIASES     ##########

# some more aliases
#alias ll='ls -l'
#alias la='ls -A'
#alias l='ls -CF'
#alias lsa='ls -la'

# Other general aliases
alias cls='clear'
alias ls='exa --icons'
alias bat='bat --style=auto'
alias lsa='ls -la'
alias cat='bat'
alias ts-stat='sudo tailscale status'
alias reload="source ~/.zshrc && echo 'Recarregando zshrc'"
alias df='df --human-readable'


#Auto execu√ß√£o ao abrir o terminal
fastfetch   #mostra as informa√ß√µes do sistema
#alias       #mostra os comandos personalizados do shell
